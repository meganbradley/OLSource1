---
title: "Monitoring Performance of Natively Compiled Stored Procedures"
ms.custom: na
ms.date: 07/04/2016
ms.prod: sql-server-2016
ms.reviewer: na
ms.suite: na
ms.technology: 
  - database-engine-imoltp
ms.tgt_pltfrm: na
ms.topic: article
ms.assetid: 55548cb2-77a8-4953-8b5a-f2778a4f13cf
caps.latest.revision: 12
manager: jhubbard
---
# Monitoring Performance of Natively Compiled Stored Procedures
This topic discusses how you can monitor the performance of natively compiled stored procedures  
  
## Using Extended Events  
 Use the **sp_statement_completed** extended event to trace execution of a query. Create an extended event session with this event, optionally with a filter on object_id for a particular natively compiled stored procedure, The extended event is raised after the execution of each query. The CPU time and duration reported by the extended event indicate how much CPU the query used and the execution time. A natively compiled stored procedure that uses a lot of CPU time may have performance problems.  
  
 **line_number**, along with the **object_id** in the extended event can be used to investigate the query. The following query can be used to retrieve the procedure definition. The line number can be used to identify the query within the definition:  
  
```tsql  
select [definition] from sys.sql_modules where object_id=object_id  
```  
  
 For more information about the **sp_statement_completed** extended event, see [How to retrieve the statement that caused an event](http://blogs.msdn.com/b/extended_events/archive/2010/05/07/making-a-statement-how-to-retrieve-the-t-sql-statement-that-caused-an-event.aspx).  
  
## Using Data Management Views  
 [!INCLUDE[ssNoVersion](../../Topics/TopicNameContainA/includes/ssNoVersion_md.md)] supports collecting execution statistics for natively compiled stored procedures, both on the procedure level and the query level. Collecting execution statistics is not enabled by default due to performance impact.  
  
 You can enable and disable statistics collection on natively compiled stored procedures using [sys.sp_xtp_control_proc_exec_stats (Transact-SQL)](assetId:///f5119808-76a1-4522-8529-9e02ee39adcb).  
  
 When statistics collection is enabled with [sys.sp_xtp_control_proc_exec_stats (Transact-SQL)](assetId:///f5119808-76a1-4522-8529-9e02ee39adcb), you can use [sys.dm_exec_procedure_stats (Transact-SQL)](assetId:///ab8ddde8-1cea-4b41-a7e4-697e6ddd785a) to monitor performance of a natively compiled stored procedure.  
  
 When statistics collection is enabled with [sys.sp_xtp_control_query_exec_stats (Transact-SQL)](assetId:///4838125d-ad1e-479e-b7d2-42655e8f4f02), you can use [sys.dm_exec_query_stats (Transact-SQL)](assetId:///eb7b58b8-3508-4114-97c2-d877bcb12964) to monitor performance of a natively compiled stored procedure.  
  
 At the start of collection, enable statistics collection. Then, execute the natively compiled stored procedure. At the end of collection, disable statistics collection. Then, analyze the execution statistics returned by the DMVs.  
  
 After you collect statistics, the execution statistics for natively compiled stored procedures can be queried for a procedure with [sys.dm_exec_procedure_stats (Transact-SQL)](assetId:///ab8ddde8-1cea-4b41-a7e4-697e6ddd785a), and for queries with [sys.dm_exec_query_stats (Transact-SQL)](assetId:///eb7b58b8-3508-4114-97c2-d877bcb12964).  
  
> [!NOTE]  
>  For natively compiled stored procedures when statistics collection is enabled, worker time is collected in milliseconds. If the query executes in less than a millisecond, the value will be 0. For natively compiled stored procedures, **total_worker_time** may not be accurate if many executions take less than 1 millisecond.  
  
 The following query returns the procedure names and execution statistics for natively compiled stored procedures in the current database, after statistics collection:  
  
```tsql  
select object_id,  
       object_name(object_id) as 'object name',  
       cached_time,  
       last_execution_time,  
       execution_count,  
       total_worker_time,  
       last_worker_time,  
       min_worker_time,  
       max_worker_time,  
       total_elapsed_time,  
       last_elapsed_time,  
       min_elapsed_time,  
       max_elapsed_time   
from sys.dm_exec_procedure_stats  
where database_id=db_id() and object_id in (select object_id   
from sys.sql_modules where uses_native_compilation=1)  
order by total_worker_time desc  
```  
  
 The following query returns the query text as well as execution statistics for all queries in natively compiled stored procedures in the current database for which statistics have been collected, ordered by total worker time, in descending order:  
  
```tsql  
select st.objectid,   
       object_name(st.objectid) as 'object name',   
       SUBSTRING(st.text, (qs.statement_start_offset/2) + 1, ((qs.statement_end_offset-qs.statement_start_offset)/2) + 1) as 'query text',   
       qs.creation_time,  
       qs.last_execution_time,  
       qs.execution_count,  
       qs.total_worker_time,  
       qs.last_worker_time,  
       qs.min_worker_time,  
       qs.max_worker_time,  
       qs.total_elapsed_time,  
       qs.last_elapsed_time,  
       qs.min_elapsed_time,  
       qs.max_elapsed_time  
from sys.dm_exec_query_stats qs cross apply sys.dm_exec_sql_text(sql_handle) st  
where  st.dbid=db_id() and st.objectid in (select object_id   
from sys.sql_modules where uses_native_compilation=1)  
order by qs.total_worker_time desc  
```  
  
 Natively compiled stored procedures support SHOWPLAN_XML (estimated execution plan). The estimated execution plan can be used to inspect the query plan, to find any bad plan issues. Common reasons for bad plans are:  
  
-   Stats were not updated before the procedure was created.  
  
-   Missing indexes  
  
 Showplan XML is obtained by executing the following [!INCLUDE[tsql](../../Topics/TopicNameContainA/includes/tsql_md.md)]:  
  
```tsql  
SET SHOWPLAN_XML ON  
GO  
EXEC my_proc   
GO  
SET SHOWPLAN_XML OFF  
GO  
```  
  
 Alternatively, in [!INCLUDE[ssManStudioFull](../../Topics/TopicNameContainA/includes/ssManStudioFull_md.md)], select the procedure name and click **Display Estimated Execution Plan**.  
  
 The estimated execution plan for natively compiled stored procedures shows the query operators and expressions for the queries in the procedure. [!INCLUDE[ssSQL14](../../Topics/TopicNameContainA/includes/ssSQL14_md.md)] does not support all SHOWPLAN_XML attributes for natively compiled stored procedures. For example, attributes related to query optimizer costing are not part of the SHOWPLAN_XML for the procedure.  
  
## See Also  
 [Natively Compiled Stored Procedures](../../Topics/TopicNameNotContainA/Natively-Compiled-Stored-Procedures.md)